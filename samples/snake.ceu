#define DIM 31

emit WINDOW_SET_TITLE("Snake");
emit WINDOW_SET_SIZE(DIM*10,DIM*10,DIM,DIM);

data Direction;
data Direction.Left;
data Direction.Right;
data Direction.Up;
data Direction.Down;

code/await Snake (none) -> (event void grow) -> void do
    var Point head = val Point(0,0);

    var[(DIM*DIM)*] Point body = [head];

    code/tight Is_Free (var& Point pt1) -> yesno do
        var usize i;
        loop i in [0 -> $outer.body[ do
            var Point pt2 = outer.body[i];
            if pt2.x==pt1.x and pt2.y==pt1.y then
                escape no;
            end
        end
        escape yes;
    end

    var Direction dir = val Direction.Right();
    var Direction nxt = val Direction.Right();

    var bool to_grow = false;
    spawn do
        every grow do
            to_grow = true;
        end
    end

    par do
        var integer key;
        every key in KEY_PRESS do
            if      key==KEY_LEFT  and
                    not ((dir is Direction.Left)  or (dir is Direction.Right))
            then
                nxt = val Direction.Left();
            else/if key==KEY_RIGHT and
                    not ((dir is Direction.Right) or (dir is Direction.Left))
            then
                nxt = val Direction.Right();
            else/if key==KEY_UP    and
                    not ((dir is Direction.Up)    or (dir is Direction.Down))
            then
                nxt = val Direction.Up();
            else/if key==KEY_DOWN  and
                    not ((dir is Direction.Down)  or (dir is Direction.Up))
            then
                nxt = val Direction.Down();
            end
        end
    with
        every 250ms do
            dir = nxt;
            var Point head = body[$body-1];
            if dir is Direction.Left then
                head.x = head.x - 1;
            else/if dir is Direction.Right then
                head.x = head.x + 1;
            else/if dir is Direction.Down then
                head.y = head.y - 1;
            else/if dir is Direction.Up then
                head.y = head.y + 1;
            end

            body = body .. [head];
            if to_grow then
                to_grow = false;
            else
                $body = $body - 1;
            end
        end
    with
        every FRAMES_REDRAW do
            var usize i;
            loop i in [0 -> $body[ do
                var Point pt = body[i];
                emit GRAPHICS_SET_COLOR_NAME(COLOR_WHITE);
                emit GRAPHICS_DRAW_PIXEL(pt.x,pt.y);
            end
        end
    with
        loop do
            await FRAMES_UPDATE;
            var Point head = body[$body-1];
            if head.x<(-DIM/2) or head.x>DIM/2 or head.y<(-DIM/2) or head.y>DIM/2 then
                escape;
            end
            var integer i;
            loop i in [0 -> ($body-2) as integer] do
                var Point pt = body[i];
                if pt.x==head.x and pt.y==head.y then
                    escape;
                end
            end
        end
    end
end

code/await Food (var&? Snake snake) -> (var Point pt) -> void do
    loop do
        pt = val Point(-DIM/2 + (_rand()%(DIM+1)), -DIM/2 + (_rand()%(DIM+1)));
        if call snake!.Is_Free(&pt) then
            break;
        end
    end

    every FRAMES_REDRAW do
        emit GRAPHICS_SET_COLOR_NAME(COLOR_RED);
        emit GRAPHICS_DRAW_PIXEL(pt.x,pt.y);
        emit GRAPHICS_SET_COLOR_NAME(COLOR_WHITE);
    end
end

loop do
    emit FRAMES_SET(no);
    emit WINDOW_CLEAR();
    emit GRAPHICS_SET_FONT("tiny.ttf", 10);
    emit GRAPHICS_SET_WRITE_CURSOR(-DIM/2+1, DIM/2);
    emit GRAPHICS_WRITELN("PRESS");
    emit GRAPHICS_WRITELN(" ANY ");
    emit GRAPHICS_WRITELN(" KEY ");
    await KEY_PRESS;

    emit FRAMES_SET(yes);

    var&? Snake snake = spawn Snake();

    pool[] Food foods;
    par/or do
        await snake;
    with
        every 5s do
            spawn Food(&snake) in foods;
        end
    with
        every FRAMES_UPDATE do
            var&? Food food;
            loop food in foods do
                if not call snake!.Is_Free(&food!.pt) then
                    emit snake!.grow;
                    kill food;
                    break;
                end
            end
        end
    end
end
